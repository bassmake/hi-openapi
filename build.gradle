buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  ext {
    openapiVersion = '4.0.0-beta3'
  }
  dependencies {
    classpath "gradle.plugin.org.openapitools:openapi-generator-gradle-plugin:4.0.0-beta3"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:2.1.4.RELEASE"
  }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "org.openapi.generator"

group 'sk.bsmk'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile "org.springframework.boot:spring-boot-starter-web"
  compile "org.openapitools:openapi-generator:$openapiVersion"
  compile "org.openapitools:jackson-databind-nullable:0.1.0"
  compile "io.springfox:springfox-swagger2:2.9.2"
}

openApiGenerate {
  generatorName = "spring"
  inputSpec = "$rootDir/src/main/resources/rest-api.yaml".toString()
  outputDir = "$buildDir/generated".toString()
  apiPackage = "sk.bsmk.api"
  invokerPackage = "sk.bsmk.api.invoker"
  modelPackage = "sk.bsmk.api.model"
  configOptions = [
    dateLibrary: "java8"
  ]
}
compileJava.dependsOn tasks.openApiGenerate

idea {
  module {
    // Marks the already(!) added srcDir as "generated"
    sourceDirs += file("$buildDir/generated/src/main/java")
  }
}

wrapper {
  gradleVersion = '5.4.1'
  distributionType = Wrapper.DistributionType.ALL
}

